plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.5.31'
}
group 'io.github.siosio'
version '1.1.0'

apply plugin: 'maven-publish'
apply plugin: 'signing'

repositories {
    mavenCentral()
}

dependencies {
  compileOnly "org.jetbrains.kotlin:kotlin-stdlib"
  api "com.github.tomakehurst:wiremock-jre8:2.31.0"

  testImplementation(platform("org.junit:junit-bom:5.8.1"))
  testImplementation("org.junit.jupiter:junit-jupiter-api")
  testImplementation("org.assertj:assertj-core:3.21.0")

  testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine")

  testImplementation "org.testcontainers:junit-jupiter:1.16.2"

  testImplementation 'net.javacrumbs.json-unit:json-unit-assertj:2.28.0'
  testImplementation 'io.github.rybalkinsd:kohttp:0.12.0'
}

test {
  useJUnitPlatform()
}

compileKotlin {
  kotlinOptions {
    jvmTarget = "11"
  }
}
compileTestKotlin {
  kotlinOptions {
    jvmTarget = "11"
  }
}

java {
  withSourcesJar()
  withJavadocJar()
}

publishing {
  repositories {
    maven {
      credentials {
        username System.getenv("USER_NAME")
        password System.getenv("PASSWORD")
      }

      if (project.version.endsWith("-SNAPSHOT")) {
        url = "https://s01.oss.sonatype.org/content/repositories/snapshots/"
      } else {
        url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
      }
      allowInsecureProtocol = true
    }
  }

  publications {
    mavenJava(MavenPublication) {
      from components.java
      pom {
        name = 'wiremockk'
        url = 'https://github.com/siosio/wiremockk'
        description = 'Kotlin DSL library for WireMock.'
        licenses {
          license {
            name = 'MIT Licens'
            url = 'http://www.opensource.org/licenses/mit-license.php'
          }
        }
        developers {
          developer {
            id = 'siosio'
            name = 'siosio'
            email = 'siosio12@gmail.com'
          }
        }
        scm {
          connection = 'scm:git:git@github.com:siosio/wiremockk.git'
          developerConnection = 'scm:git:git@github.com:siosio/wiremockk.git'
          url = 'https://github.com/siosio/wiremockk'
        }
      }
    }
  }
}

signing {
  sign publishing.publications.mavenJava
}
